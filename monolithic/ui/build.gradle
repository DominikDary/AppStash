import java.util.concurrent.Executors
import java.util.concurrent.TimeUnit

apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'jmeter'

idea {
    module {
        name = 'ui'
    }
}

sourceSets {
    main {
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/main/webapp'
            srcDir 'src/main/java'
        }
    }
    test {
        resources {
            srcDir 'src/test/java'
            srcDir 'src/test/resources'
            srcDir 'src/test/load'
        }
    }
    uitest {
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
        groovy {
            srcDir 'src/uitest/groovy'
        }
    }
}

configurations {
    uitestCompile.extendsFrom testCompile
    uitestRuntime.extendsFrom testRuntime
}

repositories {
    jcenter()
}

dependencies {
    compile "net.logstash.logback:logstash-logback-encoder:2.8"

    compile springSecurityDeps
    compile wicketDeps
    compile webAppDeps

    compile project(':agent')
    compile project(':monolithic:service:authentication')
    compile project(':monolithic:service:cart')
    compile project(':monolithic:service:checkout')
    compile project(':monolithic:service:order')
    compile project(':monolithic:service:product')
    compile project(':monolithic:service:recommendation')
    compile project(':monolithic:service:user')
    // for generating sample data automatically on startup
    compile project(':monolithic:common:dataloader')
    compile "com.github.fakemongo:fongo:$fongoVersion"
    //

    testCompile jettyDeps
    testCompile testDeps

    //If using Spock, need to depend on geb-spock
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile "org.gebish:geb-junit4:$gebVersion"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"

    // If using JUnit, need to depend on geb-junit (3 or 4)
    testCompile "xerces:xercesImpl:2.8.1"
    testCompile "xml-apis:xml-apis:1.3.04"
    testCompile "org.apache.httpcomponents:httpclient:4.3.2"
}

[jettyRun, jettyRunWar]*.with {
    httpPort = 8080
    stopPort = 8081
    contextPath = "shop"
}

test {
    exclude '**/*Spec'
}

task uiTest(type: Test) {
    finalizedBy "uiTestJettyStop"

    testClassesDir = sourceSets.uitest.output.classesDir
    classpath = sourceSets.uitest.runtimeClasspath

    reports {
        html.destination = reporting.file("$name/tests")
        junitXml.destination = file("$buildDir/test-results/$name")
    }

    systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
    systemProperty "geb.env", "firefox"

    // If you wanted to set the baseUrl in your buildâ€¦
    // systemProperty "geb.build.baseUrl", "http://myapp.com"

    doFirst {
        systemProperty "spring.profiles.active", "test"
        systemProperty "logback.configurationFile", "logback-test.groovy"

        jettyRun.httpPort = 8888    // Port for test
        jettyRun.stopPort = 8889    // Port for test
        jettyRun.daemon = true
        jettyRun.execute()
    }
}

task uiTestJettyStop {
    jettyStop.stopPort = 8889
    jettyStop.stopKey = 'stopKey'
    jettyStop.execute()
}

jmeterRun.configure {
    jmeterPropertyFile = file("${projectDir}/src/test/load/jmeter.properties")
    jmeterTestFiles = [file("src/test/load/loadtest.jmx")]
}

war {
    archiveName = "monolithic-shop.war"
    def manifestClasspath = configurations.compile.collect { it.getName() }.join(',')
    manifest {
        attributes("Manifest-Version"       : "1.0",
                "Created-By"             : vendor,
                "Specification-Title"    : appName,
                "Specification-Version"  : version,
                "Specification-Vendor"   : vendor,
                "Implementation-Title"   : appName,
                "Implementation-Version" : version,
                "Implementation-Vendor"  : vendor,
                "Class-Path"             : manifestClasspath)
    }
}